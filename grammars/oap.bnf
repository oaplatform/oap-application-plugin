{
    generate = [ token-accessors="yes" ]

    parserClass="oap.application.plugin.gen.parser.OapParser"
    parserUtilClass="oap.application.plugin.parser.OapParserUtil"

    implements="oap.application.plugin.psi.OapCompositeElement"
    extends="oap.application.plugin.psi.impl.OapCompositeElementImpl"

    elementTypeHolderClass="oap.application.plugin.gen.OapTypes"
    elementTypePrefix="OAP_"
    elementTypeClass="oap.application.plugin.psi.OapCompositeElementType"
    tokenTypeClass="oap.application.plugin.psi.OapTokenType"

    psiClassPrefix="Oap"
    psiImplClassSuffix="Impl"
    psiPackage="oap.application.plugin.gen.psi"
    psiImplPackage="oap.application.plugin.gen.psi.impl"

    psiImplUtilClass="oap.application.plugin.psi.impl.GrammarPsiImplUtil"


    tokens = [
        eq="="
        leftbrace="{"
        rightbrace="}"
        leftbracket="["
        rightbracket="]"
        leftparen="("
        rightparen=")"
        leftangle='<'
        rightangle='>'
        comma=','
        dollar='$'

        duration='regexp:[0-9]+(kb|mb|gb|ms|s|m|h|d|w)'

        dot='.'
        dash='-'

        bool='regexp:true|false'

        id_modules='modules'
        id_serives='services'
        id_kernel='kernel'
        id_this='this'
        id_self='self'

        id_enabled='enabled'
        id_abstract='abstract'
        id_default='default'
        id_name='name'
        id_depends_on='dependsOn'
        id_include='include'
        id_required='required'
        id_services='services'
        id_parameters='parameters'
        id_implementation='implementation'
        id_remote='remote'
        id_url='url'
        id_serialization='serialization'
        id_configurations='configurations'
        id_loader='loader'
        id_config='config'
        id_listen='listen'
        id_link='link'
        id_sessionaware='sessionAware'
        id_interceptors='interceptors'
        id_path='path'
        id_port='port'
        id_blocking='blocking'
        id_compression='compression'
        id_ws_service='ws-service'
        id_ws_handler='ws-handler'
        id_supervision='supervision'
        id_supervise='supervise'
        id_schedule='schedule'
        id_thread='thread'
        id_delay='delay'
        id_cron='cron'
        id_kernel='kernel'

        key_value="regexp:\w+"

        nextline='regexp:\r\n|\n'
        string='regexp:"(\\"|[^"])*"'
        comment='regexp:(//|#)[^\n]+'
    ]
}

module ::=
        []
        module_name_pair
        ( &'enabled' module_enabled )?
        ( &'include' module_include )?
        ( &'dependsOn' module_depends_on )?
        ( &'include' module_include )?
        ( &'services' module_services )?
        ( &'include' module_include )?
        ( &'configurations' module_configurations )? {
    pin = 1
    recoverWhile=recover_module
}


class_name_psi ::= class_name {
    mixin="oap.application.plugin.psi.OapClassValueMixin"
}

env_value ::=  '$' '{' key_value '}'
id_value ::=  env_value id_value? | key_value id_value?
function ::= &(id_value '(') id_value '(' id_value ')'

bool_value ::= bool {
    methods=[getBooleanValue]
    implements="oap.application.plugin.psi.OapValue"
}

any_reference_in ::= &'modules' reference_modules_value_in | &'services' reference_services_in | &'kernel' reference_kernel_value_in
any_reference ::= '<'  any_reference_in '>' {
    pin=1
    recoverWhile=recover_reference
}
reference_type_kernel ::= [] 'kernel'  { pin=1 }
reference_kernel_value_in ::= reference_type_kernel '.' 'self' { pin=1 }
reference_kernel_value ::= '<' reference_kernel_value_in '>' {
    pin=1
    implements="oap.application.plugin.psi.OapValue"
}
reference_type_modules ::= [] 'modules' { pin=1 }
reference_modules_name ::= [] (reference_value | 'this') {
    pin=1
    methods=[getReference]
    implements="oap.application.plugin.psi.IModuleName"
}
reference_modules_service_name ::= [] reference_value {
    pin=1
    implements="oap.application.plugin.psi.IServiceName"
    methods=[getReference]
}
reference_modules_value_in ::= reference_type_modules '.' reference_modules_name '.' reference_modules_service_name { pin=1 }
reference_modules_value ::= '<' reference_modules_value_in '>' {
    pin=1
    methods=[getReferenceModuleName getReferenceServiceName]
    implements="oap.application.plugin.psi.OapValue"
}
reference_type_services ::= [] 'services'  { pin=1 }
reference_type_services_self ::= [] 'self' { pin=1 }
reference_type_services_property ::= [] reference_value { pin=1 }
reference_services_in ::= reference_type_services '.' reference_type_services_self '.' reference_type_services_property {pin=1}
reference_services_value ::= '<' reference_services_in '>' {
    pin=1
    implements="oap.application.plugin.psi.OapValue"
}

module_name ::= [] key_value {
    pin=1
    implements="oap.application.plugin.psi.IModuleName"
}
module_name_pair ::= [] id_name eq module_name {
    pin=1
    extends="oap.application.plugin.psi.impl.OapModuleNameBaseImpl"
    implements="oap.application.plugin.psi.OapKeyValuePair"
    stubClass="oap.application.plugin.stub.OapModuleNameStub"
    elementTypeFactory="oap.application.plugin.psi.impl.OapElementTypeFactory.factory"
}

// enabled =
module_enabled ::= 'enabled' '=' bool {
    pin=1
    recoverWhile=recover_module
}
// enabled =

// dependsOn = [
module_depends_on_name ::= key_value {
    implements=["oap.application.plugin.psi.IndentNormal";"oap.application.plugin.psi.IModuleName"]
    methods=[getReference]
}
module_depends_on ::= 'dependsOn' '=' ('[' (module_depends_on_name (','? module_depends_on_name )* )? ']' | module_depends_on_name) {
    pin=1
    recoverWhile=recover_module
}
// ] // dependsOn

// services {
module_services ::= 'services' '{' module_services_service* '}' {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePair"
}
service_name ::= key_name
module_services_service ::= service_name ( &dot dot_implementation_service | object_service ) {
    pin=1
    mixin="oap.application.plugin.psi.OapModuleServicesServiceMixin"
}

private dot_implementation_service ::= '.' module_services_service_implementation {
    pin=1
}
private object_service ::= '{' module_services_service_entities '}' {
    pin=1
}

private module_services_service_entities ::=
    ( &'enabled' module_services_service_enabled )?
    ( &'abstract' module_services_service_abstract )?
    module_services_service_implementation
    ( &'dependsOn' module_services_service_dependson )?
    ( &'default' module_services_service_default )?
    ( &'listen' module_services_service_listen )?
    ( &'parameters' module_services_service_parameters )?
    ( &'link' module_services_service_link )?
    ( &'remote' module_services_service_remote)?
    ( &'ws-service' module_services_service_wsservice)?
    ( &'ws-handler' module_services_service_wshandler)?
    ( &'supervision' module_services_service_supervision )?


module_services_service_enabled ::= 'enabled' '=' bool_value {
    pin=1
    mixin = "oap.application.plugin.psi.OapModuleServicesServiceEnabledMixin"
}




// dependsOn = [
module_services_service_dependson_name ::= key_value {
    implements=["oap.application.plugin.psi.IndentNormal";"oap.application.plugin.psi.IServiceName"]
    methods=[getReference]
}
module_services_service_dependson ::= 'dependsOn' '=' ('[' module_services_service_dependson_name? ( ','? module_services_service_dependson_name )* ']' | module_services_service_dependson_name)* {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
// ] // dependsOn

module_services_service_abstract ::= 'abstract' '=' bool_value {pin=1}

module_services_service_implementation ::= 'implementation' '=' class_name_psi {
    pin=1
    mixin="oap.application.plugin.psi.OapModuleServicesServiceImplementationMixin"
}

module_services_service_default ::= 'default' '=' reference_modules_value {pin=1 }
// } // services


// parameters {
module_services_service_parameters ::= 'parameters' ( &'.' dot_parameters | parameters_object )  {
    pin=1
    mixin="oap.application.plugin.psi.OapModuleServicesServiceParametersMixin"
}
private dot_parameters ::= '.' parameter_key_value
parameters_object ::= '{' parameter_key_value? (','? parameter_key_value )* '}' {pin=1}
parameters_array ::= '[' parameters_array_item* ']' {pin=1}
parameters_array_item ::= &'<' any_reference | &'{' parameters_object | ( id_value (',' id_value )* ) {
    implements="oap.application.plugin.psi.IndentNormal"
}

parameter_key_value ::=
      &'include' module_include
    | parameter_key_value_key {
    mixin="oap.application.plugin.psi.OapModuleServicesServiceParameterKeyValueMixin"
}
private parameter_key_value_key ::= key_name ('.' key_name)* (&'=' parameter_key_value_key_eq | parameter_key_value_key_object ) { pin=1 }
private parameter_key_value_key_eq ::= '=' ( &'[' parameters_array | &'<' any_reference | function | bool_value | id_value ) { pin=1 }
private parameter_key_value_key_object ::= parameters_object
// } parameters


// listen|link {
module_services_service_listen ::= 'listen' ( &'.' dot_module_services_service_link_field | module_services_service_link_field_object) {
    pin=1
}

dot_module_services_service_link_field ::= '.' module_services_service_link_field {pin=1}
module_services_service_link_field_object ::= '{' module_services_service_link_field* '}' {pin=1}

module_services_service_link_field ::= key_name '=' reference_modules_value {pin=1}

module_services_service_link ::= 'link' ( &'.' dot_module_services_service_link_field | module_services_service_link_field_object ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
// } // listen|link



// supervision {
module_services_service_supervision ::= 'supervision' ( &'.' dot_supervision | supervision_object ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

private dot_supervision ::= '.' module_services_service_supervision_entities {pin=1}
private supervision_object ::= '{' module_services_service_supervision_entities* '}' {pin=1}

private module_services_service_supervision_entities ::=
      &'supervise' module_services_service_supervision_entities_supervise
    | &'schedule' module_services_service_supervision_entities_schedule
    | &'thread' module_services_service_supervision_entities_thread
    | &'delay' module_services_service_supervision_entities_delay
    | &'cron' module_services_service_supervision_entities_cron

module_services_service_supervision_entities_supervise ::= 'supervise' eq bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

module_services_service_supervision_entities_schedule ::= 'schedule' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

module_services_service_supervision_entities_thread ::= 'thread' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

module_services_service_supervision_entities_delay ::= 'delay' '=' key_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

module_services_service_supervision_entities_cron ::= 'cron' '=' key_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
// } supervision


// remote {
module_services_service_remote ::= 'remote' (&'.' dot_remote | remote_object ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
dot_remote ::= '.' remote_name
remote_object ::= '{' remote_entries '}' {pin=1}
remote_entries ::= (&'name' remote_name | &'timeout' remote_timeout | &'serialization' remote_serialization | &'url' remote_url )*

remote_name ::= 'name' '=' reference_modules_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
    recoverWhile=recover_remote
}

remote_url ::= 'url' '=' key_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

remote_timeout_id_value ::= 'timeout'
remote_timeout ::= remote_timeout_id_value eq duration {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
    recoverWhile=recover_to_end_of_line
}

remote_serialization ::= 'serialization' '=' key_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

private recover_remote ::= !('name' | 'url' | '}' | 'serialization' | 'timeout' | '>' )
// } // remote


// ws-service|ws-handler {
module_services_service_wsservice ::= 'ws-service' ( &'.' dot_wsservice_path | wsservice_object ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
dot_wsservice_path ::= '.' wsservice_path {pin=1}
wsservice_object ::= '{' wsservice_entries? '}' {pin=1}
wsservice_entries ::= (&'path' wsservice_path | &'port' wsservice_port| &'sessionAware' wsservice_sessionAware| &'interceptors' wsservice_interceptors | &'enabled' wsservice_enabled)*

wsservice_path ::= 'path' '=' (&'[' wsservice_path_multiple | key_value ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

wsservice_port ::= 'port' '=' key_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

wsservice_sessionAware ::= 'sessionAware' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

wsservice_enabled ::= 'enabled' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

wsservice_interceptors ::= 'interceptors' '=' (&'[' wsservice_interceptor_multiple|wsservice_interceptor_one) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

private wsservice_path_multiple ::= '[' key_value (!']' ','? key_value)* ']'

wsservice_interceptor_one ::= reference_modules_value {
    implements="oap.application.plugin.psi.IndentNormal"
}
private wsservice_interceptor_multiple ::= '[' wsservice_interceptor_one ( ','? wsservice_interceptor_one )* ']' {pin=1}


module_services_service_wshandler ::= 'ws-handler' ( &'.' dot_wsservice_path | wshandler_object ) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
wshandler_object ::= '{' wshandler_entries? '}' {pin=1}
wshandler_entries ::= (&'path' wsservice_path | &'port' wsservice_port| &'compression' wshandler_compression | &'blocking' wshandler_blocking | &'enabled' wsservice_enabled)*
wshandler_compression ::= 'compression' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
wshandler_blocking ::= 'blocking' '=' bool_value {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

// } // ws-service | ws-handler


// configurations = [
module_configurations ::= 'configurations' '=' '[' module_configuration* ']' {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePair"
}
module_configuration ::= '{' module_configuration_entries '}' {
    pin=2
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}
module_configuration_entries ::= [] module_configuration_entries_loader module_configuration_entries_config {
    pin=1
}

module_configuration_entries_loader ::= 'loader' '=' class_name_psi {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

module_configuration_entries_config ::= [] 'config' ('=' config_array | config_object) {
    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

config_array ::= '[' config_array_item? (','? config_array_item)* ']' {
    pin=1
}
config_array_item ::= &'{' config_object | key_value {
    implements="oap.application.plugin.psi.IndentNormal"
}
config_object ::= '{' (configuration_key_value_pair)* '}' {
    pin=1
    implements="oap.application.plugin.psi.IndentNormal"
}

configuration_key_value_pair ::=
    ( &'include' module_include )
    | key_name ('.' key_name)* ('=' (&'[' config_array | bool_value | id_value ) | config_object ) {

    pin=1
    implements="oap.application.plugin.psi.OapKeyValuePairWithIndentNormal"
}

// ] // configurations

// include required("resource")
module_include ::= 'include' 'required' '(' include_resource_name ')' {
    pin=1
}
// include required("resource")


private recover_module ::= !( 'enabled' | 'name' | 'dependsOn' | 'include' | 'services' | 'configurations' )
private recover_reference ::= !('}' | ']' | ',' | '<' | 'link' | 'remote' | 'ws-service' | 'supervision' | key_name )
